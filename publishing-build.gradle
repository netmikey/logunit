
apply plugin: 'maven-publish'
apply plugin: 'signing'

// Configure these in your $USER_HOME/.gradle/gradle.properties
if (!project.hasProperty("sonatypeUsername")) {
    ext.sonatypeUsername = ''
}
if (!project.hasProperty("sonatypePassword")) {
    ext.sonatypePassword = ''
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "${project.group}:${project.name}"
                description = "${project.description}"
                url = 'https://github.com/netmikey/logunit'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Mike Meessen'
                        email = 'netmikey@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/netmikey/logunit.git'
                    developerConnection = 'scm:git:ssh://github.com:netmikey/logunit.git'
                    url = 'https://github.com/netmikey/logunit'
                }
            }

            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
